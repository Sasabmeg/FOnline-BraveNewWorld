//
// FOnline: 2238
// Rotators
//
// skills.fos
//

// This script contains global handlers for use of some skills.

#include "_macros.fos"
#include "MsgStr.h"
#include "utils_h.fos"
#include "reputations_h.fos"
#include "_colors.fos"
#include "_vars.fos"

#define _XpForNextSkill              # (skill)  (skill > 175 ? 180 : (skill > 150 ? 120 : (skill > 125 ? 90 : (skill > 100 ? 60 : (skill > 75 ? 40 : (skill > 50 ? 30 : (skill > 25 ? 20 : 10)))))))


#define SKILLS_DEBUG_LOGGING_ALLOWED     (true)

void SkillsLog(string@ s) {
    if (SKILLS_DEBUG_LOGGING_ALLOWED) {
        Log("SKILLS: " + s);
    }
}


Item@ SelectSkillItem(Critter& cr, Item@ item, uint16 skill)
{
    Item@ handItem = _CritGetItemHand(cr);
    if(valid(handItem) && handItem.Proto.Misc_ToolSkillNum == skill)
        return handItem;
    return item;
}

Item@ SelectSkillItemExt(Critter& cr, Item@ item, uint16 skill)
{
    Item@ handItem = _CritGetItemHandExt(cr);
    if(valid(handItem) && handItem.Proto.Misc_ToolSkillNum == skill)
        return handItem;
    return item;
}


bool UseFirstAidOnCritter(Critter& cr, Critter& targetCr, Item@ item)
{
/*
    if(valid(cr.GetMap()) && cr.GetMap().IsTurnBased())
    {
        cr.Say(SAY_NETMSG, "This skill cannot be used during turn-based combat.");
        return true;
    }
*/

    bool isSelf = (targetCr.Id == cr.Id);
    bool isMedic = cr.Perk[PE_MEDIC] > 0;
    bool isFieldMedic = cr.Perk[PE_FIELD_MEDIC] > 0;

    if (targetCr.IsDead()) {
        cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_NORESSURECT);
        return true;
    }

    if (!_CritNeedFirstAid(targetCr)) {
        if (_CritNeedDoctor(targetCr)) {
            cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_NOFIRSTAID_NEEDDOCTOR);
        } else if (isSelf) {
            cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_NONEED_FIRSTAID);
        }
        return true;
    }

    if (targetCr.Timeout[TO_SK_FIRST_AID] > 0) {
        if (cr.IsPlayer())
            cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_WEARINESS);
        return false;
    }

    if (!isSelf && cr.Param[MODE_HIDE] != 0 && cr.GetAccess() < ACCESS_TESTER) {
        cr.ParamBase[MODE_HIDE] = 0;
    }

    int firstAidSkill = cr.Skill[SK_FIRST_AID];
    int itemFactor = 0;

    @item = SelectSkillItem(cr, item, SK_FIRST_AID);
    if (!valid(item)) {
        @item = SelectSkillItemExt(cr, item, SK_FIRST_AID);
    }
    if (valid(item)) {
        firstAidSkill += item.Proto.Misc_ToolSkillBonus;

        GameVar@ firstAidToolUseVar = GetLocalVar(LVAR_doctor_use, cr.Id);
        if (valid(firstAidToolUseVar)) {
            firstAidToolUseVar = firstAidToolUseVar + 1;
            int firstAidToolMaxUse = 5 + (firstAidSkill / 20) + cr.PerkBase[PE_MEDIC] * 5 + cr.PerkBase[PE_FIELD_MEDIC] * 5;
            if (firstAidToolUseVar >= firstAidToolMaxUse) {
                firstAidToolUseVar = 0;
                cr.DeleteItem(item.GetProtoId(), 1);
                cr.Say(SAY_NETMSG, "You emptied and used up all the resources in your medical tool.");
            } else {
                if (firstAidToolUseVar.GetValue() < firstAidToolMaxUse / 4) {
                    cr.Say(SAY_NETMSG, "You used up some of your medical supplies.");
                } else if (firstAidToolUseVar.GetValue() < firstAidToolMaxUse / 2) {
                    cr.Say(SAY_NETMSG, "You used up almost half of your medical supplies.");
                } else if (firstAidToolUseVar.GetValue() < firstAidToolMaxUse * 3 / 4) {
                    cr.Say(SAY_NETMSG, "You used up more than half of your medical supplies.");
                } else {
                    cr.Say(SAY_NETMSG, "You used up most of your medical supplies.");
                }
            }
        }
        itemFactor = 10;
    }

    int curHp = targetCr.Stat[ST_CURRENT_HP];
    int maxHp = targetCr.Stat[ST_MAX_LIFE];
    int heal = 0;
    int healFactor = 100;
    if (isSelf) {
        healFactor = 30;
    } else {
        healFactor = 45;
    }
    if (isMedic) {
        healFactor += 5;
    }
    if (isFieldMedic) {
        healFactor += 5;
    }
    healFactor += itemFactor;

    heal = CLAMP((maxHp - curHp) * healFactor / 100, 0, 20 + (cr.SkillBase[SK_FIRST_AID] / 4) + (isMedic ? 25 : 0) + (isFieldMedic ? 25 : 0) + itemFactor * 3);
    if (curHp + heal > maxHp) {
        heal = maxHp - curHp;
    }
    //  BNW MECHANICS CHANGE: see issue #110 https://github.com/Sasabmeg/FOnline-BraveNewWorld/issues/110
    //  Plasma damage will reduce healing for a short period of time
    if (targetCr.ParamBase[TO_EX_IRREMEDIABLE] + PLASMA_EFFECT_LINGER_TIME > int(GetTick())) {
        if (targetCr.ParamBase[ST_EX_IRREMEDIABLE] > 0) {
            int healReduce = CLAMP(targetCr.ParamBase[ST_EX_IRREMEDIABLE], 0, 75);
            int mdc = cr.Perk[PE_MEDIC] > 0 ? 1 : 0;
            int fmdc = cr.Perk[PE_FIELD_MEDIC] > 0 ? 1 : 0;
            healReduce = (healReduce * (3 - mdc - fmdc)) / 3;
            float healFactor = float(CLAMP(100 - healReduce, 25, 100)) / 100;
            int oldAmount = heal;
            heal = heal * healFactor;
            //cr.Say(SAY_NETMSG, "Heal factor = " + healFactor + ", healReduce = " + healReduce + ", irremediable% = " + targetCr.ParamBase[ST_EX_IRREMEDIABLE] + ", oldAmount = " + oldAmount + ", newAmount = " + heal);
            //Log("Heal factor = " + healFactor + "healReduce = " + healReduce + ", irremediable% = " + targetCr.ParamBase[ST_EX_IRREMEDIABLE] + ", oldAmount = " + oldAmount + ", newAmount = " + heal);
        }
    }
    //  BNW MECHANICS CHANGE END

    if (heal < 1)
        heal = 1;

    targetCr.StatBase[ST_CURRENT_HP] += heal;
    //  if TB then no heal over time from FA
    if (valid(cr.GetMap()) && !cr.GetMap().IsTurnBased()) {
        targetCr.EraseTimeEvents(CTE_FIRST_AID);
        targetCr.AddTimeEvent("cte_FirstAid", REAL_SECOND(5), CTE_FIRST_AID, 4 * 10000 + heal / 4);
    }
    //  Floating healing text.
    FloatingHealText(targetCr, heal, HEAL_TEXT_REASON_FIRSTAID, SKILL_CHECK_SUCCESS);

    cr.Say(SAY_NETMSG, "First Aid: +" + heal);
    if (!isSelf) {
        targetCr.Say(SAY_NETMSG, "First Aid: +" + heal);
    }
    _SetTimeout(targetCr, TO_SK_FIRST_AID, FIRST_AID_TIMEOUT(cr));

    int healXpFactor = 1;
    if (targetCr.IsNpc()) {
        healXpFactor *= 3;
    }
    if (isFieldMedic) {
        healXpFactor *= 2;
    } else if (isMedic) {
        healXpFactor *= 1.5;
    }
    cr.StatBase[ST_EXPERIENCE] += heal * healXpFactor;
    if (_IsTrueNpc(targetCr) && cr.Id != targetCr.Id && IsHumanoid(targetCr) && !IsCurrentEnemy(targetCr, cr.Id)) {
        if (_ValidReputationIndex(targetCr.Stat[ST_TEAM_ID])) {
            AddReputation(cr, targetCr.Stat[ST_TEAM_ID], heal / 20, REP_FIRST_AID_CAP);
        }
        if (cr.IsSeenBy(targetCr)) {
            targetCr.Say(SAY_NORM_ON_HEAD, "Thank you.");
        }
    }

    if (cr.IsPlayer()) {
        AddScore(cr, SCORE_DOCTOR, 1);
    }

    return true;
}

uint cte_FirstAid(Critter& cr, int identifier, uint& rate) {
    //Log("cte_FirstAid :: identifier = " + identifier + ", rate = " + rate);
    int heal = rate % 10000;
    int curHp = cr.Stat[ST_CURRENT_HP];
    int maxHp = cr.Stat[ST_MAX_LIFE];
    if (curHp + heal > maxHp) {
        heal = maxHp - curHp;
    }
    if (heal <= 0) {
        return 0;
    }
    cr.StatBase[ST_CURRENT_HP] += heal;
    FloatingHealText(cr, heal, HEAL_TEXT_REASON_FIRSTAID, SKILL_CHECK_SUCCESS);
    cr.Say(SAY_NETMSG, "First Aid regeneration: +" + heal);
    if (cr.Stat[ST_CURRENT_HP] >= cr.Stat[ST_MAX_LIFE]) {
        return 0;
    }
    if ((rate / 10000) - 1 <= 0) {
        return 0;
    } else {
        rate = (((rate / 10000) - 1) * 10000) + heal;
        return REAL_SECOND(5);
    }
}

bool UseDoctorOnCritter(Critter& cr, Critter& targetCr, Item@ item)
{
/*
    if(valid(cr.GetMap()) && cr.GetMap().IsTurnBased())
    {
        cr.Say(SAY_NETMSG, "This skill cannot be used during turn-based combat.");
        return true;
    }
*/

    bool isSelf = (targetCr.Id == cr.Id);
    bool isKo = targetCr.IsKnockout() && targetCr.Stat[ST_CURRENT_HP] > 0;
    bool isInjured = _CritNeedDoctor(targetCr);
    bool isMedic = cr.Perk[PE_MEDIC] > 0;
    bool isFieldMedic = cr.Perk[PE_FIELD_MEDIC] > 0;

    if(targetCr.IsDead()) {
        cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_NORESSURECT);
        return true;
    }

    if (!isInjured && !isKo) {
        if(_CritNeedFirstAid(targetCr))
            cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_NODOCTOR_NEEDFIRSTAID);
        else if(isSelf)
            cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_NONEED_DOCTOR);
        return true;
    }

    if (targetCr.Timeout[TO_SK_DOCTOR] > 0) {
        if (isKo && isMedic ) {     //can heal KO even with cooldown with Medic perk
            targetCr.StatBase[ST_CURRENT_AP] = 0;

            if (!isSelf && cr.Param[MODE_HIDE] != 0 && cr.GetAccess() < ACCESS_TESTER)
                cr.ParamBase[MODE_HIDE] = 0;
        }

        if (cr.IsPlayer() && isInjured)
            cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_WEARINESS);
        return true;
    }

    if (!isSelf && cr.Param[MODE_HIDE] != 0 && cr.GetAccess() < ACCESS_TESTER)
        cr.ParamBase[MODE_HIDE] = 0;

    int doctorSkill = cr.Skill[SK_DOCTOR];

    @item = SelectSkillItem(cr, item, SK_DOCTOR);
    if (!valid(item)) {
        @item = SelectSkillItemExt(cr, item, SK_DOCTOR);
    }

    int itemFactor = 1;
    if (valid(item)) {
        doctorSkill += item.Proto.Misc_ToolSkillBonus;
        GameVar@ doctorToolUseVar = GetLocalVar(LVAR_doctor_use, cr.Id);
        if (valid(doctorToolUseVar)) {
            doctorToolUseVar = doctorToolUseVar + 1;
            int doctorToolMaxUse = 5 + (doctorSkill / 20) + cr.PerkBase[PE_MEDIC] * 5 + cr.PerkBase[PE_FIELD_MEDIC] * 5;
            if (doctorToolUseVar >= doctorToolMaxUse) {
                doctorToolUseVar = 0;
                cr.DeleteItem(item.GetProtoId(), 1);
                cr.Say(SAY_NETMSG, "You emptied and used up all the resources in your medical tool.");
            } else {
                if (doctorToolUseVar.GetValue() < doctorToolMaxUse / 4) {
                    cr.Say(SAY_NETMSG, "You used up some of your medical supplies.");
                } else if (doctorToolUseVar.GetValue() < doctorToolMaxUse / 2) {
                    cr.Say(SAY_NETMSG, "You used up almost half of your medical supplies.");
                } else if (doctorToolUseVar.GetValue() < doctorToolMaxUse * 3 / 4) {
                    cr.Say(SAY_NETMSG, "You used up more than half of your medical supplies.");
                } else {
                    cr.Say(SAY_NETMSG, "You used up most of your medical supplies.");
                }
            }

        }
        itemFactor = 3;
    }

    int uninjured = 0;
    int injured = 0;
    int difficulty = 1;
    difficulty += _CritHasInjureBody(cr, DAMAGE_LEFT_ARM) ? 1 : 0;
    difficulty += _CritHasInjureBody(cr, DAMAGE_RIGHT_LEG) ? 1 : 0;
    difficulty += _CritHasInjureBody(cr, DAMAGE_EYE) ? 1 : 0;

    for (int i = DAMAGE_EYE; i <= DAMAGE_LEFT_LEG; ++i) {
        if (!_CritHasInjureBody(targetCr, i))
            continue;
        injured++;
        int roll = Random(5, 95) * ((isSelf && !isMedic) ? 2 : 1) * difficulty / 2 / itemFactor;
        if(doctorSkill > roll) {
            _CritUninjureBody(targetCr, i);
            cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_HEAL_DMG(i - DAMAGE_POISONED));
            uninjured++;
        } else {
            cr.SayMsg(SAY_NETMSG, TEXTMSG_GAME, STR_SKILL_NOHEAL_DMG(i - DAMAGE_POISONED));
        }
        doctorSkill -= 20 + (isFieldMedic ? 0 : (isMedic ? 15 : 30));
    }

    if (uninjured == 0 && injured > 0 && isSelf && !isMedic) {
        int curHp = targetCr.Stat[ST_CURRENT_HP];
        int hurt = curHp * Random(3, 7 * injured) / 100;
        targetCr.StatBase[ST_CURRENT_HP] -= hurt;
        targetCr.Action(CRITTER_ACTION_DAMAGE_FORCE, 1, null);
        targetCr.Say(SAY_NETMSG, "|" + COLOR_DORANGE + " While trying to heal your injuries you hurt yourself for |" + COLOR_ORANGE + " " + hurt + "|" + COLOR_DORANGE + "  damage.");
    }

    if (isKo) { //heals KO
        targetCr.StatBase[ST_CURRENT_AP] = (isFieldMedic ? 5 : (isMedic ? 3 : 0));
    }

    if (isInjured || (isKo && isMedic)) {
        _SetTimeout(targetCr, TO_SK_DOCTOR, DOCTOR_TIMEOUT(cr));
    }

    if (uninjured > 0)
    {
        if (isSelf)
            VerboseAction(cr, "applies wound dressing" + (uninjured > 1 ? "s" : ""));
        else
            VerboseAction(cr, targetCr, "treats injur" + (uninjured > 1 ? "ies" : "y") + " of TARGET");
        cr.StatBase[ST_EXPERIENCE] += uninjured * 100 * (isFieldMedic ? 5 : (isMedic ? 3 : 1));
        if (_IsTrueNpc(targetCr) && !isSelf && IsHumanoid(targetCr) && !IsCurrentEnemy(targetCr, cr.Id)) {
            if (_ValidReputationIndex(targetCr.Stat[ST_TEAM_ID]))
                AddReputation(cr, targetCr.Stat[ST_TEAM_ID], uninjured * 8, REP_DOCTOR_CAP);
            if (cr.IsSeenBy(targetCr))
                targetCr.Say(SAY_NORM_ON_HEAD, "Thank you.");
        }
    } else {
        /*if( isSelf )
                VerboseAction( cr, "TODO uninjured=0 isSelf" );
           else
                VerboseAction( cr, targetCr, "TODO uninjured=0 TARGET" );*/
    }

    if (cr.IsPlayer() && isInjured) {
        AddScore(cr, SCORE_DOCTOR, uninjured);
    }
    return true;
}

void GainSkillXp(Critter& cr, uint skillNum, int xp) {
    SkillsLog("GainSkillXp called :: skillNum = " + skillNum + ", xp = " + xp);
    int xpForNextSkill = 0;
    uint xpSkillVarNum = 0;
    int oldSkill = cr.Skill[skillNum];
    switch (skillNum) {
        case SK_LOCKPICK:
            xpSkillVarNum = LVAR_skill_xp_lockpick;
            break;
        case SK_STEAL:
            xpSkillVarNum = LVAR_skill_xp_steal;
            break;
        case SK_SCIENCE:
            xpSkillVarNum = LVAR_skill_xp_science;
            break;
        case SK_REPAIR:
            xpSkillVarNum = LVAR_skill_xp_repair;
            break;
        case SK_SPEECH:
            xpSkillVarNum = LVAR_skill_xp_speech;
            break;
        case SK_BARTER:
            xpSkillVarNum = LVAR_skill_xp_barter;
            break;
        case SK_GAMBLING:
            xpSkillVarNum = LVAR_skill_xp_gambling;
            break;
        case SK_OUTDOORSMAN:
            xpSkillVarNum = LVAR_skill_xp_outdoors;
            break;
        default:
            SkillsLog("GainSkillXp :: Not valid skill.");
            return;
    }

	GameVar@ xpVar = GetLocalVar(xpSkillVarNum, cr.Id);
	if (!valid(xpVar)) {
        SkillsLog("Invalid variable = " + xpSkillVarNum);
        return;
	}
	if (xpVar.GetValue() > _XpForNextSkill(cr.Skill[skillNum])) {
        xpVar = 0;
	}

    while (xp > 0 && cr.SkillBase[skillNum] < 200) {
        xpForNextSkill = _XpForNextSkill(cr.SkillBase[skillNum]);
        SkillsLog("GainSkillXp :: " + SkillName(skillNum) +  " currentXp = " + xpVar.GetValue() + ", gainXp = " + xp);
        if (xpVar.GetValue() + xp < xpForNextSkill) {
            xpVar = xpVar.GetValue() + xp;
            xp = 0;
            SkillsLog("GainSkillXp (" + SkillName(skillNum) + ") :: no skill gain: xp = " + xp + ", xpVar = " + xpVar.GetValue() + ", neededForNext = " + xpForNextSkill + ", skillValue = " + cr.Skill[skillNum]);
        } else {
            xp -= xpForNextSkill - xpVar.GetValue();
            xpVar = 0;
            cr.SkillBase[skillNum] += 1;
            SkillsLog("GainSkillXp (" + SkillName(skillNum) + ") :: skill gain: xp = " + xp + ", xpVar = " + xpVar.GetValue() + ", neededForNext = " + xpForNextSkill + ", skillValue = " + cr.Skill[skillNum]);
        }
    }
    if (cr.SkillBase[skillNum] > 200) {
        cr.SkillBase[skillNum] = 200;
    }
    if (oldSkill < cr.Skill[skillNum]) {
        string message = "Your " + SkillName(skillNum) + " skill has increased by " + (cr.SkillBase[skillNum] - oldSkill) + ".";
        cr.Say(SAY_NETMSG, message);
        cr.RunClientScript("client_utils@_playSound_unsafe", 0, 0, 0, "levelup.acm", null);
    }
}

int GetCraftXpByPid(uint pid) {
    switch (pid) {
        case PID_METAL_ORE:
        case PID_MINERAL:
        case PID_GUNPOWDER:
        case PID_MEAT_JERKY:
        case PID_FLINT:
        case PID_BRAHMINSHIT:
            return 1;
        case PID_ROT_GUT:
        case PID_FIBER:
        case PID_FIREWOOD:
        case PID_PUMP_PARTS:
        case PID_METAL_PARTS:
        case PID_XANDER_ROOT:
        case PID_BROC_FLOWER:
        case PID_MUTATED_FRUIT:
        case PID_MEAT:
        case PID_ROPE:
        case PID_GECKO_PELT:
        case PID_TOBACCO:
        case PID_CHEMICALS:
        case PID_HYPODERMIC_NEEDLE:
        case PID_MINERALS2:
            return 2;
        case PID_BIO_GEL:
        case PID_BRAHMIN_HIDE:
        case PID_GOLDEN_GECKO_PELT:
        case PID_ALLOYS:
        case PID_ELECTRONIC_PARTS:
        case PID_ORE2:
        case PID_GUNPOWDER2:
            return 3;
        case PID_FIBRE2:
        case PID_CHEMICALS2:
            return 4;
        case PID_FIREGECKO_PELT:
        case PID_ELECTRONIC_PARTS2:
        case PID_METAL_PARTS2:
        case PID_ALLOYS2:
        case PID_URANIUM_ORE:
            return 5;
        default:
            return 0;
    }
    return 0;
}

uint GetCraftingSkillNumByCraftPid(Item& item) {
    const ProtoItem@ proto = item.Proto;
    int craftSkillNum = 0;

    if (proto.Type == ITEM_TYPE_WEAPON) {
        int weaponSkillNum = proto.Weapon_Skill_0;
        switch (weaponSkillNum) {
            case SK_ENERGY_WEAPONS:
                craftSkillNum = SK_SCIENCE;
                break;
            default:
                craftSkillNum = SK_REPAIR;
        }
        switch (proto.ProtoId) {
            case PID_CATTLE_PROD:
            case PID_SUPER_CATTLE_PROD:
            case PID_MEGA_POWER_FIST:
            case PID_POWER_FIST:
                craftSkillNum = SK_SCIENCE;
                break;
            case PID_SPEAR:
            case PID_SHARPENED_POLE:
            case PID_SHARP_SPEAR:
            case PID_KNIFE:
            case PID_THROWING_KNIFE:
            case PID_MOLOTOV_COCKTAIL:
            case PID_SLEDGEHAMMER:
            case PID_SHOVEL:
                craftSkillNum = SK_OUTDOORSMAN;
                break;
            default:
        }
        SkillsLog("proto weaponSkillNum = " + weaponSkillNum + ", name = " + SkillName(weaponSkillNum));
    } else if (proto.Type == ITEM_TYPE_ARMOR) {
        switch (proto.ProtoId) {
            case PID_LEATHER_ARMOR:
            case PID_LEATHER_ARMOR_HELMET:
            case PID_LEATHER_JACKET:
            case PID_LEATHER_JACKET_HELMET:
                craftSkillNum = SK_OUTDOORSMAN;
                break;
            case PID_TESLA_ARMOR:
            case PID_TESLA_HELMET:
                craftSkillNum = SK_SCIENCE;
                break;
            default:
                craftSkillNum = SK_REPAIR;
        }
        SkillsLog("proto ARMOR craftskill = " + craftSkillNum);
    } else if (proto.Type == ITEM_TYPE_DRUG) {
        switch (proto.ProtoId) {
            case PID_WEAK_HEALING_POWDER:
            case PID_HEALING_POWDER:
            case PID_ANTIDOTE:
                craftSkillNum = SK_OUTDOORSMAN;
                break;
            default:
                craftSkillNum = SK_SCIENCE;
        }
        SkillsLog("proto DRUG craftskill = " + craftSkillNum);
    } else if (proto.Type == ITEM_TYPE_AMMO) {
        switch (proto.ProtoId) {
            case PID_SMALL_ENERGY_CELL:
            case PID_MICRO_FUSION_CELL:
                craftSkillNum = SK_SCIENCE;
                break;
            default:
                craftSkillNum = SK_REPAIR;
        }
        SkillsLog("proto AMMO craftskill = " + craftSkillNum);
    } else {
        switch (proto.ProtoId) {
            case PID_LOCK:
            case PID_LOCKPICKS:
            case PID_EXP_LOCKPICK_SET:
            case PID_ELECTRONIC_LOCKPICKS:
            case PID_ELEC_LOCKPICK_MKII:
                craftSkillNum = SK_LOCKPICK;
                break;
            case PID_AXE:
            case PID_WATERBAG:
            case PID_WATERBAG_EMPTY:
            case PID_TENT:
            case PID_PRIMITIVE_TOOL:
            case PID_RADIO:
            case PID_ROPE:
                craftSkillNum = SK_OUTDOORSMAN;
                break;
            default:
                craftSkillNum = SK_REPAIR;
        }
        SkillsLog("proto ELSE craftskill = " + craftSkillNum);
    }

    return craftSkillNum;
}
