//
// FOnline: 2238
// Rotators
//
// client_keybinds_h.fos
//

import bool GUI_IsKeyPressed(uint8 key) from "client_gui";
import void SetLastMode(uint8 mode) from "client_mapper_animation";
import bool ToggleWallRoofTransparency() from "client_interface";
import void ToggleInventoryTracker() from "client/client_InventoryTracker";
import bool IsInventoryTrackerEnabled() from "client/client_InventoryTracker";
import void ToggleCarTracker() from "client/client_CarTracker";
import bool IsCarTrackerEnabled() from "client/client_CarTracker";
import void BeepCarTracker() from "client/client_CarTracker";
import void ToggleGuiImage() from "client/client_GuiImage";
import bool IsGuiImageEnabled() from "client/client_GuiImage";

shared interface IBind
{
    bool Active();
    void Exec();
};

shared interface IBindExecFunc // "function pointer"
{
    void Exec();
};

class BaseBind : IBind
{
    bool ctrl;
    bool alt;
    bool shift;
    BaseBind()
    {
        ctrl = false;
        alt = false;
        shift = false;
    }

    bool CtrlPressed()  { return GUI_IsKeyPressed(DIK_LCONTROL) || GUI_IsKeyPressed(DIK_RCONTROL); }
    bool ShiftPressed() { return GUI_IsKeyPressed(DIK_LSHIFT) || GUI_IsKeyPressed(DIK_RSHIFT); }
    bool AltPressed()   { return GUI_IsKeyPressed(DIK_LMENU) || GUI_IsKeyPressed(DIK_RMENU); }

    bool Active()
    {
        return((ctrl == CtrlPressed()) && (alt == AltPressed()) && (shift == ShiftPressed()));
    }

    void Exec() {}
};

shared class BindList
{
    array<IBind@> binds;

    BindList()
    {
        binds.resize(0);
    }

    bool TryRun()
    {
        if(__ConsoleActive)
            return(false);
        bool ret = false;
        for(uint i = 0, j = binds.length(); i < j; i++)
            if(binds[i].Active())
            {
                binds[i].Exec();
                ret = true;
            }
        return ret;
    }

    void AddBind(IBind@ bind)
    {
        binds.insertLast(bind);
    }
};

class Bind : IBind
{
    array<int> modifiers;

    bool Active()
    {
        for(uint i = 0; i < modifiers.length(); i++)
            if(!GUI_IsKeyPressed(modifiers[i]))
                return false;

        return true;
    }

    void Exec() {}
};

class GMBind : Bind
{
    array<string> commands;

    void Exec()
    {
        for(uint i = 0; i < this.commands.length(); i++)
        {
            string entry = this.commands[i];

            array<string@>@ command = split(entry, " ");

            if(command[0] == "play")
            {
                string @rec = GetRecord(command[1]);
                if(valid(rec))
                {
                    Message("Playing...");
                    StartReplay(rec);
                }
                else
                    Message("Recording not valid, you sure the file exists?");
            }
            else
                RunServerScriptUnsafe("cheats@unsafe_ExecCommand", 0, 0, 0, "command " + entry, null);
        }
    }
};

class UseBind : BaseBind
{
    array<uint16> pids;
    void Exec()
    {
        CritterCl@ cr = GetChosen();
        ItemCl@    item = null;
        for(uint i = 0; i < pids.length(); i++)
        {
            @item = cr.GetItem(pids[i], -1);
            if(valid(item))
                break;
        }
        if(!valid(item))
            return;
        uint[] action = { CHOSEN_USE_ITEM, item.Id, item.GetProtoId(), TARGET_SELF, cr.Id, ITEM_MODE_USE, 0 };
        SetChosenActions(action);
    }
};

class StopCritterBind : BaseBind
{
    void Exec()
    {
        array<uint> actions;
        uint        cnt = GetChosenActions(actions);
        if(actions.length() > 0)// && actions[0] == CHOSEN_MOVE)
        {
            uint[] action = { CHOSEN_NONE, 0, 0, 0, 0, 0, 0 };
            SetChosenActions(action);
        }
    }
};

class ReloadBind : BaseBind
{
    void Exec()
    {
        CritterCl@ cr = GetChosen();
        ItemCl@    item = cr.GetItem(0, SLOT_HAND1);
        if(!valid(item))
            return;
        uint[] Reload = { CHOSEN_USE_ITEM, item.Id, 0, TARGET_SELF_ITEM, 0, ITEM_MODE_RELOAD, 0 };

        if(item.GetType() == ITEM_TYPE_WEAPON && item.AmmoCount < item.Proto.Weapon_MaxAmmoCount)
        {
            uint mode = item.Mode;
            RunServerScriptUnsafe("unsafe_client@unsafe_WeaponMode", mode, 0, 0, "", null);
            SetLastMode(mode);
            SetChosenActions(Reload);
        }
    }
};

class PickupBind : BaseBind
{
    void Exec()
    {
        RunServerScriptUnsafe("unsafe_client@unsafe_PickAll", 0, 0, 0, "", null);
    }
};

class DropallBind : BaseBind
{
    void Exec()
    {
        RunServerScriptUnsafe("unsafe_client@unsafe_DropAll", 0, 0, 0, "", null);
    }
};

class AimBind : BaseBind
{
    uint8 aim;
    AimBind(uint8 _aim)
    {
        this.aim = _aim;
    }
    void Exec()
    {
        CurrentAim = this.aim;
    }
};

class ToggleFogBind : BaseBind
{
    void Exec()
    {
        __ConfigFog = !__ConfigFog;
    }
};

class ToggleRoofBind : BaseBind
{
    void Exec()
    {
        __ShowRoof = !__ShowRoof;
        Message("Show roofs: "+__ShowRoof+".");
        RefreshMap(false, true, false);
    }
};

class ToggleWallBind : BaseBind
{
    void Exec()
    {
        __ShowWall = !__ShowWall;
        Message("Show walls: "+__ShowWall+".");
        RefreshMap(false, false, false);
    }
};

class ToggleMsFovBind : BaseBind
{
    void Exec()
    {
        __ConfigMsFov = !__ConfigMsFov;
        string confString = (__ConfigMsFov==true)?"on.":"off.";
        Message("Motion sensor range indicator "+confString);
    }
}

class FASelfBind : BaseBind
{
    void Exec()
    {
        CritterCl@ cr = GetChosen();
        uint[] action = { CHOSEN_USE_SKILL_ON_CRITTER, SK_FIRST_AID, cr.Id, 0, 0, 0, 0 };
        SetChosenActions(action);
    }
}

class DocSelfBind : BaseBind
{
    void Exec()
    {
        CritterCl@ cr = GetChosen();
        uint[] action = { CHOSEN_USE_SKILL_ON_CRITTER, SK_DOCTOR, cr.Id, 0, 0, 0, 0 };
        SetChosenActions(action);
    }
}

class SSOtherBind : BaseBind
{
    void Exec()
    {
        CritterCl@ cr = GetChosen();
        CritterCl@ target = GetMonitorCritter(__MouseX, __MouseY);
        if(!valid(target))
            return;

        ItemCl@ ss = cr.GetItem(PID_SUPER_STIMPAK, -1);

        if(!valid(ss))
        {
            Message("No super stimpak in inventory.");
            return;
        }

        uint[] action = { CHOSEN_USE_ITEM, ss.Id, ss.GetProtoId(), TARGET_CRITTER, target.Id, ITEM_MODE_USE, 0 };
        SetChosenActions(action);
    }
}

class CarTrackerBind : BaseBind
{
    void Exec()
    {
        ToggleCarTracker();
        if (IsCarTrackerEnabled()) {
            Message("Car Tracker enabled.");
        } else {
            Message("Car Tracker disabled.");
        }
    }
}

class CarTrackerBeepBind : BaseBind
{
    void Exec()
    {
        BeepCarTracker();
    }
}

class GuiImageBind : BaseBind {
    void Exec()     {
        ToggleGuiImage();
        if (IsGuiImageEnabled()) {
            Message("GUI Image enabled.");
        } else {
            Message("GUI Image disabled.");
        }
    }
}

class InventoryTrackerBind : BaseBind
{
    void Exec()
    {
        ToggleInventoryTracker();
        if (IsInventoryTrackerEnabled()) {
            Message("Inventory Tracker enabled.");
        } else {
            Message("Inventory Tracker disabled.");
        }
    }
}

class DischargeBind : BaseBind
{
    void Exec()
    {
        RunServerScriptUnsafe("unsafe_client@unsafe_Discharge", 0, 0, 0, "", null);
    }
}

class ScienceAtHexBind : BaseBind
{
    void Exec()
    {
        RunServerScriptUnsafe("unsafe_client@unsafe_ScienceAtHex", 0, 0, 0, " ", null);
    }
};

class CenterChosenBind : BaseBind
{
    void Exec()
    {
        CritterCl@ cr = GetChosen();
        if(valid(cr))
            MoveScreen(cr.HexX, cr.HexY, 0);
    }
};

class TransparencyBind : BaseBind
{
	void Exec()
	{
		bool result = ToggleWallRoofTransparency();
		Message("Walls/roofs transparency: "+result+".");
	}
}
