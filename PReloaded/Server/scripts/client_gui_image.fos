/**
*   FO version:     FOClassic: Brave New World
*   Contributor:    Sasabazmeg
*   File:           client_gui_map.fos
*
*   Description:    Pop-up an image of a drawn map for quests or dungeons.
*/

#ifndef __GUI_IMAGE__
#define __GUI_IMAGE__

#include "sprite.fos"
#include "_defines.fos"
#include "_client_defines.fos"
#include "_macros.fos"
#include "_colors.fos"
#include "client_gui_h.fos"
#include "client_InterfaceControl.fos"
#include "client_InterfaceTextbox.fos"
#include "client_InterfaceWindow.fos"
#include "client_InterfaceImage.fos"


CGuiImage guiImage;
CInterfaceWindow interfaceWindow;
CInterfaceImage interfaceImage;

//  Exported
void ToggleGuiImage() {
    guiImage.enabled = !guiImage.enabled;
}

// Exported
bool IsGuiImageEnabled() {
    return guiImage.enabled;
}

//  Exported - client_main
void InitGuiImage() {
    Log("InitGuiImage()");
    Message("InitGuiImage()");
    guiImage = CGuiImage();
    interfaceWindow = CInterfaceWindow(0, 0, 700, 405);
    interfaceWindow.maxWidth = __ScreenWidth * 4 / 5;
    interfaceWindow.maxHeight = __ScreenHeight * 4 / 5;
    interfaceImage = CInterfaceImage(0, 0, 1400, 810);
    interfaceImage.hasBackground = false;
    /*
    if (interfaceImage.SetImage("sierra_caves_lvl1_map1.png")) {
        Log("Setting image: sierra_caves_lvl1_map1.png");
        Message("Setting image: sierra_caves_lvl1_map1.png");
    } else {
        Log("Failed setting image: sierra_caves_lvl1_map1.png");
        Message("Failed setting image: sierra_caves_lvl1_map1.png");
    }
    */
    interfaceWindow.childControls.insertLast(interfaceImage);
}

//  Exported - client_main
bool SetImage(string& imageFile, bool limitWindowSizeMaxToImageSize, bool resizeEnabled, bool moveEnabled) {
    if (interfaceImage.SetImage(imageFile)) {
        Log("Setting image: " + imageFile);
        Message("Setting image: " + imageFile);
        interfaceWindow.allowResize = resizeEnabled;
        interfaceWindow.allowMove = moveEnabled;
        interfaceWindow.totalWidth = interfaceImage.spriteWidth + interfaceWindow.borderSizeLeft + interfaceWindow.borderSizeRight;
        interfaceWindow.totalHeight = interfaceImage.spriteHeight + interfaceWindow.borderSizeTop + interfaceWindow.borderSizeBottom;
        if (limitWindowSizeMaxToImageSize) {
            interfaceWindow.maxWidth = interfaceImage.spriteWidth + interfaceWindow.borderSizeLeft + interfaceWindow.borderSizeRight;
            interfaceWindow.maxHeight = interfaceImage.spriteHeight + interfaceWindow.borderSizeTop + interfaceWindow.borderSizeBottom;
        }
        guiImage.enabled = true;
        return true;
    } else {
        Log("Failed setting image: " + imageFile);
        Message("Failed setting image: " + imageFile);
        return false;
    }
}

//  Exported
void DrawGuiImage() {
    guiImage.Draw();
}

//  Exported
bool GuiImageCatchMouseClick(int click) {
    if (!guiImage.enabled) {
        return false;
    }
    CritterCl@ chosen = GetChosen();
    if(valid(chosen)) {
        //Message("Gui Image CatchMouseClick Button: " + click);
        if (GetCurrentCursor() == CURSOR_DEFAULT && click == MOUSE_CLICK_RIGHT) {
            if (interfaceWindow.isWithinDrawArea(__MouseX, __MouseY)) {
                //textBox.MouseWheelDown();
                guiImage.enabled = false;
                return true;
            } else {
                return false;
            }
        } else {
            return interfaceWindow.CatchMouseClick(click);
        }
    } else {
        return false;
    }
}

//  Exported
bool GuiImageCatchMouseRelease(int click) {
    if (!guiImage.enabled) {
        return false;
    }
    CritterCl@ chosen = GetChosen();
    if (valid(chosen)) {
        //Message("Gui Image CatchMouseRelease Button: " + click);
        return interfaceWindow.CatchMouseRelease();
    } else {
        return false;
    }
}

//  Exported
bool GuiImageUpdateMousePositionChange(int x, int y) {
    if (!guiImage.enabled) {
        return false;
    }
    //Message("Gui Image UpdateMousePositionChange (" + x + ", " + y + ")");
    return interfaceWindow.UpdateMousePositionChange(x, y);
}


class CGuiImage {
    bool enabled;                           //  use this to toggle on/off

    CGuiImage() {
        enabled = false;
    }

    void Draw() {
        if (!enabled) {
            //Message("GuiImage disabled.");
            return;
        }

        if (GUI_GetActiveMainScreen() != CLIENT_MAIN_SCREEN_GAME) {
            //Message("GuiImage not on main screen.");
            return;
        }

        CritterCl@ chosen = GetChosen();
        if (!valid(chosen)) {
            //Message("Chosen is fake!");
            return;
        }
        interfaceWindow.DrawWindow();
    }
};

#endif 	//	__GUI_IMAGE__
